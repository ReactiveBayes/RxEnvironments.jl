"""
Simulation of DC motor.
Reference: https://www.mathworks.com/help/control/ug/dc-motor-control.html

For visualizations of motor diagram, see "dcdemofigures_01.png" and "dcdemofigures_02.png".

There are 2 inputs:      Va   = input voltage,    Td   = temporal disturbance.
There are 2 states:      x(1) = angular velocity, x(2) = torque at shaft.
There is  1 observation: y(1) = observed angular velocity.

Author: W.M. Kouw
Date: 27-nov-2023
"""

using ControlSystems
using Distributions
using LinearAlgebra
using Plots
default(label="", linewidth=4, margin=10Plots.pt)

"Define continuous-time state-space model of DC motor"

R  = 2.0    # Ohms
L  = 0.5    # Henrys
Km = 0.1    # torque constant
Kb = 0.1    # back emf constant
Kf = 0.2    # Nms
J  = 0.02   # kg.m^2/s^2

# Find transfer function of armature block
h1 = tf(Km,[L, R])

# Find transfer function of load block
h2 = tf(1, [J, Kf])

# Generate state-space model from both blocks
sys = ss(h2)*[h1 1]

# Add feedback effect of electromagnetic field generated by armature
dcm = feedback(sys, Kb, U1=1)

"Simulate response of system to designed inputs signal"

# Time
Ts = 0.1
t = 0.0:Ts:3.0

# Design a control signal
inputs(x,t) = t > 1.0 ? [1.0, 0.0] : [0.0, 0.0]

# Call simulator
y, t, x, u = lsim(dcm, inputs, t, x0=[0.0, 0.0])

"Plot results"

p1 = plot(t,x', lab=["angular velocity [ω(t)] " "torque [τ(t)]"], xlabel="", ylabel="Amplitude")
p2 = plot(t,u', lab=["Voltage [Vₐ(t)]" "Disturbance [Td(t)]"], xlabel="Time [s]", ylabel="Amplitude")
plot(p1,p2, layout=(2,1), size=(800,400))

savefig("examples/dc-motor/simulation.png")